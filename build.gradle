plugins {
    id 'org.sonarqube' version '2.5'
    id 'net.researchgate.release' version '2.6.0'
    id 'maven-publish'
}


project.ext.set( "checkstyleConfigFile", file( "${checkstyleConfigFileLocation}" ) )

sonarqube {
    properties {
        property "sonar.host.url", "${project.sonarUrl}"
    }
}

configure( [ project(':bitcoin-common'), project(':bitcoin-rest'), project(':bitcoin-soap') ] ) {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "${jacocoToolVersion}"
    }

    jacocoTestReport {
        additionalSourceDirs = files( "java" )
        reports {
            xml.setEnabled( true )
            html.setDestination( file( "${buildDir}/jacocoHtml" ) )
        }
    }

    test {
        useTestNG()
    }
}

configure( [ project(':bitcoin-common'), project(':bitcoin-rest'), project(':bitcoin-soap'), project(':bitcoin-model') ] ) {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'

    checkstyleMain {
        configFile = project.checkstyleConfigFile
    }

    checkstyleTest {
        configFile = project.checkstyleConfigFile
    }

    task javadocJar( type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
        from 'build/docs/javadoc'
    }

    publishing {
        publications {
            javaDocs( MavenPublication ) {
                artifact javadocJar
            }
        }
    }
}

configure( [ project(':bitcoin-functional-tests'), project(':bitcoin-integration-tests'), project(':bitcoin-model'),
             project(':bitcoin-wsdl') ] ) {
  sonarqube {
    skipProject = true
  }
}

configure( [ project(':bitcoin-common'), project(':bitcoin-rest'), project(':bitcoin-soap'), project(':bitcoin-model'),
             project(':bitcoin-wsdl') ] ) {
    apply plugin: 'java'
    apply plugin: 'osgi'
    apply plugin: 'maven-publish'

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    task sourceJar( type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            sources(MavenPublication) {
                artifact sourceJar
            }
            bundles(MavenPublication) {
                artifact "${buildDir}/libs/${project.artifactId}-${project.version}.jar"
            }
        }
    }
}

configure( subprojects ) { subproject ->
    apply plugin: 'java'
    repositories {
        mavenCentral()
        maven { url "${redhatMavenRepositoryUrl}" }
    }
}

tasks.withType( PublishToMavenRepository ) {
    it.dependsOn(':bitcoin-rest:generateSwaggerDocumentation')
}

allprojects {
    apply plugin: 'maven-publish'
    publishing {
        repositories {
            maven {
                url "http://${project.artifactoryHost}/artifactory/libs-${project.version.endsWith('SNAPSHOT') ? 'snapshot' : 'release'}-local"
                credentials {
                    username = "${artifactoryUsername}"
                    password = "${artifactoryPassword}"
                }
            }
        }
    }
}

publishing {
    publications {
        deplZip(MavenPublication) {
            artifact source: deployableZIP, extension: 'zip'
        }
        swagger(MavenPublication) {
            artifact source: file("${rootDir}/bitcoin-rest/build/swagger/swagger.json"), classifier: 'swagger'
        }
    }
}

configurations {
    deployable {
        transitive = false
    }
}

dependencies {
    deployable project(':bitcoin-common')
    deployable project(':bitcoin-model')
    deployable project(':bitcoin-rest')
    deployable project(':bitcoin-soap')
    deployable project(':bitcoin-wsdl')
}

task deployableZIP(type: Zip) {
    dependsOn configurations.deployable

    baseName = "bitcoin-esb-${project.version}"
    destinationDir = buildDir

    configurations.deployable.collect {
        from it
    }
}

task cleanBuildDir(type: Delete) {
  delete buildDir
}

release {
    preTagCommitMessage = '[Researchgate Release Plugin] pre tag commit:'
    tagCommitMessage = '[Researchgate Release Plugin]'
    newVersionCommitMessage = '[Researchgate Release Plugin] version commit:'
    buildTasks = ['releaseBuild', 'publish']
    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
        commitVersionFileOnly = false
    }
}

task releaseBuild {
  dependsOn(
          'cleanBuildDir',
          project('bitcoin-wsdl').clean,
          project('bitcoin-wsdl').build,
          project('bitcoin-wsdl').publish,
          project('bitcoin-model').clean,
          project('bitcoin-model').build,
          project('bitcoin-model').publish,
          project('bitcoin-common').clean,
          project('bitcoin-common').build,
          project('bitcoin-common').publish,
          project('bitcoin-soap').clean,
          project('bitcoin-soap').build,
          project('bitcoin-soap').publish,
          project('bitcoin-rest').clean,
          project('bitcoin-rest').build,
          project('bitcoin-rest').publish
)
}

task wrapper (type: Wrapper) {
    gradleVersion = '4.3'
    distributionType Wrapper.DistributionType.ALL
}

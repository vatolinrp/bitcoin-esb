buildscript {
    repositories {
        maven {
            url "http://repo1.maven.org/maven2/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.5'
    }
}

plugins {
    id 'org.sonarqube' version '2.5'
}

project.ext.set( "checkstyleConfigFile", file( "${checkstyleConfigFileLocation}" ) )

subprojects {
    apply plugin: 'java'
    apply plugin: 'osgi'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'

    test {
        useTestNG()
    }

    repositories {
        mavenCentral()
        maven { url "http://10.162.12.91/artifactory/repo" }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    jacoco {
        toolVersion = "${jacocoToolVersion}"
    }

    jacocoTestReport {
        additionalSourceDirs = files( "java" )
        reports {
            xml.setEnabled( true )
            html.setDestination( file( "${buildDir}/jacocoHtml" ) )
        }
    }

   task sourceJar( type: Jar, dependsOn: classes) {
       classifier 'sources'
       from sourceSets.main.allSource
   }

   publishing {
       publications {
           mavenJava(MavenPublication) {
               artifact sourceJar
           }
       }
       repositories {
           maven {
               url "${artifactoryURL}"
               credentials {
                   username = "${artifactoryUsername}"
                   password = "${artifactoryPassword}"
               }
           }
       }
   }
   checkstyleMain {
       configFile = project.checkstyleConfigFile
   }
   checkstyleTest {
       configFile = project.checkstyleConfigFile
   }
}

task wrapper (type: Wrapper) {
    gradleVersion = '4.1'
    distributionType Wrapper.DistributionType.ALL
}
